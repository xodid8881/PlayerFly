plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.21'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'org.hwabeag'
version = '1.21.10-1.0.0'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'placeholderapi'
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.6'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'org.xerial:sqlite-jdbc:3.46.1.3'
    implementation 'com.mysql:mysql-connector-j:9.2.0'
}

kotlin {
    jvmToolchain(21)
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = '21'
    }
}

tasks.named('jar') {
    enabled = false
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    archiveClassifier.set('')
}

tasks.named('build') {
    dependsOn tasks.named('shadowJar')
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
